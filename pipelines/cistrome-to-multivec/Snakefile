import os
from os.path import join
import json
import requests
import platform

configfile: 'config.yml'

O2_USER = config.get("user", None)

# Check if this is running on O2
IS_O2 = (platform.system() == "Linux" and O2_USER != None)

print("This is", (" " if IS_O2 else " not "), "running on O2", sep='')

# Directory / file constants
SRC_DIR = "src"
DATA_DIR = ("data" if not IS_O2 else join(os.sep, "n", "scratch3", "users", O2_USER[0], O2_USER, "cistrome-explorer", "data"))
RAW_DIR = join(DATA_DIR, "raw")
INTERMEDIATE_DIR = join(DATA_DIR, "intermediate")
PROCESSED_DIR = join(DATA_DIR, "processed")

# URL constants
SAMPLE_DATA_URL = "http://dc2.cistrome.org/api/datahub/{cid}"
SAMPLE_METADATA_URL = "http://dc2.cistrome.org/api/inspector?id={cid}"

# Process the config
#GROUP_NAMES = config["groups"].keys() # TODO: uncomment to create processed files for all available species & factors
#GROUP_NAMES = ['Homo_sapiens__AEBP2__all', 'Homo_sapiens__AFF1__all', 'Homo_sapiens__AFF4__all'] # TODO: remove
GROUP_NAMES = [ name for name, group in config["groups"].items() if len(group) < 100 and name[0] == "H" ][:200] # TODO: remove

FILETYPE = config.get("filetype", "mv5")

# Zarr outputs are really directories, so they need to be wrapped in snakemake's directory()
# function.
# https://snakemake.readthedocs.io/en/stable/snakefiles/rules.html#directories-as-outputs
OUTFILE_WRAPPER = directory if FILETYPE == "zarr" else (lambda x: x)

# Rules
rule all:
    input:
        expand(join(PROCESSED_DIR, f"{{group_name}}.multires.{FILETYPE}"), group_name=GROUP_NAMES),

# Given a manifest file (containing an array of metadata.json file paths and cooresponding bigWig file paths),
# create a single output file (either multivec or zarr format).
# Here, we can use the snakemake "groups" feature to reduce the number of cluster job submissions.
# https://snakemake.readthedocs.io/en/stable/snakefiles/rules.html?highlight=group#defining-groups-for-execution
rule manifest_to_outfile:
    group: "outfile_group"
    input:
        bigwigs=lambda w: expand(join(RAW_DIR, "{cid}.bw"), cid=config["groups"][w.group_name]),
        metadata=lambda w: expand(join(RAW_DIR, "{cid}.metadata.json"), cid=config["groups"][w.group_name]),
        manifest=join(INTERMEDIATE_DIR, "{group_name}.manifest.json")
    output:
        OUTFILE_WRAPPER(join(PROCESSED_DIR, f"{{group_name}}.multires.{FILETYPE}"))
    params:
        starting_resolution=200,
        script=join(SRC_DIR, f"manifest_to_{FILETYPE}.py")
    shell:
        """
        python {params.script} \
            -i {input.manifest} \
            -o {output} \
            -s {params.starting_resolution} \
            -n {wildcards.group_name}
        """

# Create a "manifest" file for each output group.
# This just simplifies the command line arguments for the
# manifest_to_mv5.py and manifest_to_zarr.py scripts.
rule bigwigs_to_manifest:
    group: "outfile_group"
    input:
        bigwigs=lambda w: expand(join(RAW_DIR, "{cid}.bw"), cid=config["groups"][w.group_name]),
        metadata=lambda w: expand(join(RAW_DIR, "{cid}.metadata.json"), cid=config["groups"][w.group_name]),
    output:
        join(INTERMEDIATE_DIR, "{group_name}.manifest.json")
    script:
        join(SRC_DIR, "bigwigs_to_manifest.py")

# The Cistrome DB API outputs a JSON array,
# where the first array element contains a URL to
# the bigWig file associated with the sample ID.
# We can tell snakemake that this bigwig file is "temporary",
# so that snakemake will delete it after all rules that use it as an input are completed.
# https://snakemake.readthedocs.io/en/stable/snakefiles/rules.html#protected-and-temporary-files
rule download_bigwig:
    group: "outfile_group"
    input:
        join(RAW_DIR, "{cid}.data.json")
    output:
        # 
        temp(join(RAW_DIR, "{cid}.bw"))
    shell:
        '''
        curl -s -S -L -o {output} $(cat {input} | jq '.[0].url' -r)
        '''

# The Cistrome DB has two separate endpoints for each sample:
# - a "data" endpoint which returns JSON containing the path to the bigwig file for the sample
# - a "metadata" endpoint which returns JSON metadata for the sample 
rule download_bigwig_metadata:
    group: "outfile_group"
    output:
        metadata_json=join(RAW_DIR, "{cid}.metadata.json"),
        data_json=join(RAW_DIR, "{cid}.data.json")
    params:
        sample_metadata_url=lambda w: SAMPLE_METADATA_URL.format(cid=w.cid),
        sample_data_url=lambda w: SAMPLE_DATA_URL.format(cid=w.cid)
    shell:
        '''
        curl -s -S -L -o {output.data_json} {params.sample_data_url} && \
        curl -s -S -L -o {output.metadata_json} {params.sample_metadata_url}
        '''

