import os
from os.path import join
import json
import requests
import platform

configfile: 'config.yml'

# Check if this is running on O2
is_o2 = (platform.system() == "Linux")

# Directory / file constants
SRC_DIR = "src"
DATA_DIR = ("data" if not is_o2 else join(os.sep, "n", "scratch3", "users", "m", "mk596", "cistrome-explorer", "data"))
RAW_DIR = join(DATA_DIR, "raw")
PROCESSED_DIR = join(DATA_DIR, "processed")

# URL constants
SAMPLE_DATA_URL = "http://dc2.cistrome.org/api/datahub/{cid}"
SAMPLE_METADATA_URL = "http://dc2.cistrome.org/api/inspector?id={cid}"

GROUP_NAMES = config["groups"].keys()

# Rules
rule all:
    input:
        expand(join(PROCESSED_DIR, "{group_name}.multires.mv5"), group_name=GROUP_NAMES),

rule manifest_to_multivec:
    input:
        bigwigs=lambda w: expand(join(RAW_DIR, "{cid}.bw"), cid=config["groups"][w.group_name]),
        metadata=lambda w: expand(join(RAW_DIR, "{cid}.metadata.json"), cid=config["groups"][w.group_name]),
        manifest=join(RAW_DIR, "{group_name}.manifest.json")
    output:
        multivec=join(PROCESSED_DIR, "{group_name}.multires.mv5")
    params:
        starting_resolution=1000,
        script=join(SRC_DIR, "manifest_to_multivec.py")
    shell:
        """
        python {params.script} \
            -i {input.manifest} \
            -o {output.multivec} \
            -s {params.starting_resolution}
        """

rule bigwigs_to_manifest:
    input:
        bigwigs=lambda w: expand(join(RAW_DIR, "{cid}.bw"), cid=config["groups"][w.group_name]),
        metadata=lambda w: expand(join(RAW_DIR, "{cid}.metadata.json"), cid=config["groups"][w.group_name]),
    output:
        join(RAW_DIR, "{group_name}.manifest.json")
    script:
        join(SRC_DIR, "bigwigs_to_manifest.py")

rule download_bigwig:
    input:
        join(RAW_DIR, "{cid}.data.json")
    output:
        join(RAW_DIR, "{cid}.bw")
    shell:
        '''
        curl -L -o {output} $(cat {input} | jq '.[0].url' -r)
        '''

rule download_bigwig_metadata:
    output:
        metadata_json=join(RAW_DIR, "{cid}.metadata.json"),
        data_json=join(RAW_DIR, "{cid}.data.json")
    params:
        sample_metadata_url=lambda w: SAMPLE_METADATA_URL.format(cid=w.cid),
        sample_data_url=lambda w: SAMPLE_DATA_URL.format(cid=w.cid)
    shell:
        '''
        curl -L -o {output.data_json} {params.sample_data_url} && \
        curl -L -o {output.metadata_json} {params.sample_metadata_url}
        '''

